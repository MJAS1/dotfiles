# Pass ctrl-s through applications
stty -ixon

# Antigen plugin manager
if [ -f ~/.zsh/antigen.zsh ]; then
    source ~/.zsh/antigen.zsh

    # Only autostart tmux if not running in ssh or in vscode
    if [ -z "$SSH_CLIENT" ] && [ "$TERM_PROGRAM" != "vscode" ] && command -v tmux &>/dev/null; then
        ZSH_TMUX_AUTOSTART=true
        antigen bundle tmux
    fi

    antigen bundle Aloxaf/fzf-tab
    antigen bundle zsh-users/zsh-autosuggestions
    antigen bundle zsh-users/zsh-syntax-highlighting
    antigen theme romkatv/powerlevel10k
    antigen bundle agkozak/zsh-z
    antigen bundle hlissner/zsh-autopair
    antigen apply

    # Powerlevel10k theme
    [[ ! -f ~/.zsh/p10k.zsh ]] || source ~/.zsh/p10k.zsh
fi

# fzf
if [ -f /usr/share/fzf/shell/key-bindings.zsh ]; then
    source /usr/share/fzf/shell/key-bindings.zsh
elif [ -f /usr/share/doc/fzf/examples/key-bindings.zsh ]; then
    source /usr/share/doc/fzf/examples/key-bindings.zsh
fi

# Shell environment
if [ -f ~/.env ]; then
    source ~/.env
fi

# Aliases
if [ -f ~/.aliases ]; then
    source ~/.aliases
fi

# Functions
if [ -f ~/.functions ]; then
    source ~/.functions
fi

# Local settings
if [ -f ~/.zshrc_local ]; then
    source ~/.zshrc_local
fi

reset_prompt() {
    local precmd
    for precmd in $precmd_functions; do
        $precmd
    done
    zle reset-prompt
}

# Jump up one directory level with Ctrl+Alt-U
up_one() {
  if up 1; then
      local precmd
      reset_prompt
  fi
}
zle -N up_one
bindkey '\e\C-u' up_one

# Pop directory stack with Ctrl-O
pop_d() {
  popd > /dev/null || return
  reset_prompt
}
zle -N pop_d
bindkey '^O' pop_d

# Go backward in dir stack
dir_stack_back() {
  pushd -1 > /dev/null || return
  reset_prompt
}
zle -N dir_stack_back
bindkey '^[i' dir_stack_back  # Alt-i

# Go forward in dir stack
dir_stack_forward() {
  pushd +1 > /dev/null || return
  reset_prompt
}
zle -N dir_stack_forward
bindkey '^[o' dir_stack_forward  # Alt-o

cdf() {
    local dir
    dir=$(dirs -v | fzf --height=40% --layout=reverse --border | awk '{print $2}')
    dir=${dir/#\~/$HOME}  # Expand tilde to home directory
    cd "$dir"
    reset_prompt
}
zle -N cdf
bindkey '^g' cdf

autoload edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

HISTFILE=~/.zsh_histfile
HISTSIZE=5000
SAVEHIST=10000

setopt autocd autopushd extendedglob nomatch notify histignorespace pushdignoredups
bindkey -e

# The following lines were added by compinstall
zstyle :compinstall filename '$HOME/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall
